apply plugin: 'java-library'
apply plugin: 'me.champeau.mrjar'

description = 'Neo4j Graph Data Science :: Common Neo4j Kernel Adapter 5.x'

group = 'org.neo4j.gds'

// for all 5.x versions
if (ver.'neo4j'.startsWith('5.')) {
    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java17']
            }
        }
    }

    dependencies {
        annotationProcessor project(':annotations')
        annotationProcessor group: 'org.immutables', name: 'value', version: ver.'immutables'
        annotationProcessor group: 'org.neo4j', name: 'annotations', version: ver.'neo4j'

        compileOnly project(':annotations')
        compileOnly group: 'org.immutables', name: 'value-annotations', version: ver.'immutables'
        compileOnly group: 'org.neo4j', name: 'annotations', version: ver.'neo4j'
        compileOnly group: 'org.neo4j', name: 'neo4j', version: ver.'neo4j'
        compileOnly group: 'org.neo4j', name: 'neo4j-record-storage-engine', version: ver.'neo4j'
        compileOnly group: 'org.neo4j.community', name: 'it-test-support', version: ver.'neo4j'

        implementation project(':neo4j-kernel-adapter-api')
    }
} else {
    multiRelease {
        targetVersions 11, 17
    }

    if (!project.hasProperty('no-forbidden-apis')) {
        forbiddenApisJava17 {
            exclude('**')
        }
    }

    dependencies {
        annotationProcessor group: 'org.neo4j', name: 'annotations', version: ver.'neo4j'

        compileOnly project(':annotations')
        compileOnly group: 'org.neo4j', name: 'annotations', version: ver.'neo4j'
        compileOnly group: 'org.neo4j', name: 'neo4j', version: ver.'neo4j'

        implementation project(':neo4j-kernel-adapter-api')

        java17AnnotationProcessor project(':annotations')
        java17AnnotationProcessor group: 'org.immutables', name: 'value', version: ver.'immutables'
        java17AnnotationProcessor group: 'org.neo4j', name: 'annotations', version: neos.'5.11'

        java17CompileOnly project(':annotations')
        java17CompileOnly group: 'org.immutables', name: 'value-annotations', version: ver.'immutables'
        java17CompileOnly group: 'org.neo4j', name: 'neo4j', version: neos.'5.11'
        java17CompileOnly group: 'org.neo4j', name: 'neo4j-record-storage-engine', version: neos.'5.11'
        java17CompileOnly group: 'org.neo4j.community', name: 'it-test-support', version: neos.'5.11'

        java17Implementation project(':neo4j-kernel-adapter-api')
    }
}
