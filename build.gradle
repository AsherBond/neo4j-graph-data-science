plugins {
    id 'com.github.hierynomus.license'              version '0.16.2-37dde1f'        apply false

    alias(openGds.plugins.forbiddenapis).apply(false)
    alias(openGds.plugins.google.protobuf).apply(false)
    alias(openGds.plugins.champeau.mrjar).apply(false)
    alias(openGds.plugins.nebula.dependencies).apply(false)
    alias(openGds.plugins.shadowjar).apply(false)
    alias(openGds.plugins.spawn).apply(false)
}

description = 'Neo4j Graph Data Science :: OpenGDS'

group = 'org.neo4j.gds'

ext {
    distributionDir = "${layout.buildDirectory.get()}/distributions"
    cliDistributionDir = "${layout.buildDirectory.get()}/distributions/estimation-cli"
    publicDir = "${project.rootDir}"

    checkstyleReportsDir = "${layout.buildDirectory.get()}/reports/checkstyle/"
}

apply from: 'gradle/version.gradle'

allprojects {
    apply plugin: 'base'

    group = 'org.neo4j.gds'
    version = gdsVersion

    tasks.register("ci") {
        dependsOn tasks.check
    }

    tasks.register("cipr") {
        dependsOn tasks.check
    }

    tasks.register("checkstyle") {
        group = "verification"
        description = "Run all checkstyle related tasks"
    }

}

apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/defaults-java.gradle'
apply from: 'gradle/forbidden-apis.gradle'
apply from: 'gradle/checkstyle.gradle'
apply from: 'gradle/spotless.gradle'
apply from: 'gradle/pregel-bootstrap.gradle'
