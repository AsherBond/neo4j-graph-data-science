plugins {
    id 'com.github.hierynomus.license'              version '0.16.2-37dde1f'        apply false
    id 'com.github.johnrengelman.shadow'            version '8.1.0'                 apply false
    id 'ch.kk7.spawn'                               version '1.0.20180924200750'    apply false
    id 'de.thetaphi.forbiddenapis'                  version '3.6'                   apply false
    id 'nebula.info-dependencies'                   version '11.4.1'                 apply false
    id 'com.google.protobuf'                        version '0.9.4'                apply false
    id 'me.champeau.mrjar'                          version "0.1"                   apply false
}

description = 'Neo4j Graph Data Science :: OpenGDS'

group = 'org.neo4j.gds'

ext {
    distributionDir = "$buildDir/distributions"
    cliDistributionDir = "$buildDir/distributions/estimation-cli"
    distributionLicenseFile = file('LICENSE.txt')
    publicDir = "${project.rootDir}"

    checkstyleReportsDir = "${buildDir}/reports/checkstyle/"

    compatibilityModules = [
            'kernel-adapter': [
                    project(':neo4j-kernel-adapter-4.4'),
                    project(':neo4j-kernel-adapter-5.11'),
                    project(':neo4j-kernel-adapter-5.12'),
                    project(':neo4j-kernel-adapter-5.13'),
                    project(':neo4j-kernel-adapter-5.14'),
                    project(':neo4j-kernel-adapter-5.15'),
                    project(':neo4j-kernel-adapter-5.16'),
                    project(':neo4j-kernel-adapter-5.17'),
            ],
            'storage-engine-adapter': [
                    project(':storage-engine-adapter-4.4'),
                    project(':storage-engine-adapter-5.11'),
                    project(':storage-engine-adapter-5.12'),
                    project(':storage-engine-adapter-5.13'),
                    project(':storage-engine-adapter-5.14'),
                    project(':storage-engine-adapter-5.15'),
                    project(':storage-engine-adapter-5.16'),
                    project(':storage-engine-adapter-5.17'),
            ]
    ]
}

apply from: 'gradle/version.gradle'

allprojects {
    apply plugin: 'base'

    group = 'org.neo4j.gds'
    version = gdsVersion

    tasks.register("ci") {
        dependsOn tasks.check
    }

    tasks.register("cipr") {
        dependsOn tasks.check
    }

    tasks.register("checkstyle") {
        group = "verification"
        description = "Run all checkstyle related tasks"
    }

}

apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/defaults-java.gradle'
apply from: 'gradle/forbidden-apis.gradle'
apply from: 'gradle/checkstyle.gradle'
apply from: 'gradle/spotless.gradle'
apply from: 'gradle/pregel-bootstrap.gradle'
