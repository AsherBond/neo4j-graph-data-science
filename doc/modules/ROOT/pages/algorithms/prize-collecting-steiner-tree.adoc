[[algorithms-prize-steiner-tree]]
= Minimum Directed Steiner Tree
:description: This section describes the Prize-Collecting Steiner Tree algorithm in the Neo4j Graph Data Science library.
:entity: relationship
:result: weight
:algorithm: Prize-Collecting Steiner Tree algorithm

:undirected:
:weighted:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


== Introduction

A spanning tree is a graph such that there is exactly one path between any two nodes in the set.
A graph can have many possible spanning tree subsets depending on the set of nodes/relationships selected.

Given a weighted graph where each node has a prize, the Prize-Collecting Steiner Tree problem asks for the  spanning tree that  satisfies the following conditions:

- the sum of prizes for the nodes in the graph is mximized.
- the sum of weights of relationships and prizes for nodes not in the tree is minimized.

The two constraints can combined to form a single maximization problem by simpling  subtracting the second constraint for the former.

The Prize-Collecting Steiner Tree is NP-Complete and no efficient exact algorithms is known.
The Neo4j GDS Library implements a practical   https://people.csail.mit.edu/ludwigs/papers/dimacs14_fastpcst.pdf[2-approximate algorithm] from the literature.
This means that the returned answer should be at least half as good as the optimal answer.

[[algorithms-prize-steiner-tree-considerations]]
== Considerations

By default, the Prize-Collecting Steiner Tree problem considers prizes only for nodes.
In some cases, however, it can be useful to also consider prizes on relationships.
The GDS implementation can handle prizes for relationships through the following transformation:
Given a relationship with weight `w` and prize `p`,
we suggest to replace `w` with `w' = w - p`.
This should be done as a pre-processing step prior to projecting the in-memory graph.


[[algorithms-prize-steiner-tree-syntax]]
== Syntax

.Prize-collecting Steiner Tree syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.Run the algorithm in stream mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.prizeSteinerTree.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  parentId: Integer,
  weight: Float
----
include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name | Type | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/prize-steiner/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name     | Type    | Description
| nodeId   | Integer | A node in the discovered spanning tree.
| parentId | Integer | The parent of nodeId in the spanning tree or nodeId if it is equal to the source node.
| weight   | Float   | The weight of the relationship from parentId to nodeId.
|===

======

[.include-with-stats]
======
.Run the algorithm in stats mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.prizeSteinerTree.stats(
  graphName: String,
  configuration: Map
)
YIELD
  effectiveNodeCount: Integer,
  totalWeight: Float,
  sumOfPrizes: Float,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  configuration: Map
----
include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name | Type | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
include::partial$/algorithms/prize-steiner/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                      | Type    | Description
| effectiveNodeCount        | Integer | The number of nodes in the spanning tree.
| totalWeight               | Float   | The sum of the weights of the relationships in the spanning tree.
| sumOfPrizes               | Float   | The sum of prizes for the nodes in the spanning tree.
| preProcessingMillis       | Integer | Milliseconds for preprocessing the data.
| computeMillis             | Integer | Milliseconds for running the algorithm.
| configuration             | Map     | The configuration used for running the algorithm.
|===

======

[.include-with-mutate]
======
.Run the algorithm in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.prizeSteinerTree.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  effectiveNodeCount: Integer,
  totalWeight: Float,
  sumOfPrizes: Float,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  relationshipsWritten: Integer,
  configuration: Map
----
include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name | Type | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
include::partial$/algorithms/prize-steiner/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                      | Type    | Description
| effectiveNodeCount        | Integer | The number of nodes in the spanning tree.
| totalWeight               | Float   | The sum of the weights of the relationships in the spanning tree.
| sumOfPrizes               | Float   | The sum of prizes for the nodes in the spanning tree.
| preProcessingMillis       | Integer | Milliseconds for preprocessing the data.
| computeMillis             | Integer | Milliseconds for running the algorithm.
| mutateMillis              | Integer | Milliseconds for writing result data back.
| relationshipsWritten      | Integer | The number of relationships added to the in-memory graph.
| configuration             | Map     | The configuration used for running the algorithm.
|===

======

[.include-with-write]
======
.Run the algorithm in mutate mode on a named graph.
[source, cypher, role=noplay]
----
CALL gds.prizeSteinerTree.write(
  graphName: String,
  configuration: Map
)
YIELD
  effectiveNodeCount: Integer,
  totalWeight: Float,
  sumOfPrizes: Float,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  relationshipsWritten: Integer,
  configuration: Map
----
include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="3,2,3m,2,8"]
|===
| Name | Type | Default | Optional | Description
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
include::partial$/algorithms/prize-steiner/specific-configuration.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                      | Type    | Description
| effectiveNodeCount        | Integer | The number of nodes in the spanning tree.
| totalWeight               | Float   | The sum of the weights of the relationships in the spanning tree.
| sumOfPrizes               | Float   | The sum of prizes for the nodes in the spanning tree.
| preProcessingMillis       | Integer | Milliseconds for preprocessing the data.
| computeMillis             | Integer | Milliseconds for running the algorithm.
| writeMillis               | Integer | Milliseconds for writing result data back.
| relationshipsWritten      | Integer | The number of relationships written to the graph.
| configuration             | Map     | The configuration used for running the algorithm.
|===

======

====


[[algorithms-pcst-sample]]
== Examples

include::partial$/algorithms/shared/examples-named-native-note.adoc[]

:algorithm-name: {algorithm}
:graph-description: road network
:image-file: steiner-graph.svg
include::partial$/algorithms/shared/examples-intro.adoc[]


.The following will create the sample graph depicted in the figure:
[source, cypher, role=noplay setup-query]
----
CREATE (a:Place {id: 'A', prize: 5.0}),
       (b:Place {id: 'B', prize: 20.0}),
       (c:Place {id: 'C',prize: 11.0}),
       (d:Place {id: 'D',prize: 10.0}),
       (e:Place {id: 'E',prize: 8.0}),
       (f:Place {id: 'F',prize: 1.0}),
       (a)-[:LINK {cost:10}]->(f),
       (a)-[:LINK {cost:3}]->(b),
       (a)-[:LINK {cost:7}]->(e),
       (b)-[:LINK {cost:1}]->(c),
       (c)-[:LINK {cost:4}]->(d),
       (c)-[:LINK {cost:6}]->(e),
       (f)-[:LINK {cost:3}]->(d);
----

.The following will project and store a named graph:
[source, cypher, role=noplay graph-project-query]
----
MATCH (source:Place)-[r:LINK]->(target:Place)
RETURN gds.graph.project(
  'graph',
  source,
  target,
{
    sourceNodeProperties: source { .prize },
    targetNodeProperties: target { .prize },
    relationshipProperties: r { .cost }
  },
  { undirectedRelationshipTypes: ['*'] }
)
----
[[algorithms-prize-tree-examples-mem]]
=== Memory estimation

:mode: stream
include::partial$/algorithms/shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm in stream mode:
[source, cypher, role=noplay]
----
CALL gds.prizeSteinerTree.stream.estimate('graph', {
  relationshipWeightProperty: 'cost',
  prizeProperty: 'prize'
})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
RETURN nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----
.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 6         | 14                | 3897      | 561616      | "[3897 Bytes \... 548 KiB]"
|===
--


[[algorithms-prize-tree-examples-stream]]
=== Stream
:!stream-details:

include::partial$/algorithms/shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the Prize-Collecting Steiner Tree algorithm in stream mode and return results for each valid node.
[source,cypher,role=noplay]
----

CALL gds.prizeSteinerTree.stream('graph', {
  relationshipWeightProperty: 'cost',
  prizeProperty: 'prize'
})
YIELD nodeId,parentId, weight
RETURN gds.util.asNode(nodeId).id AS node, gds.util.asNode(parentId).id AS parent,weight
ORDER BY node
----

.Results
[opts="header"]
|===
| node | parent | weight
| "A"  | "B"    | 3.0
| "B"  | "C"    | 1.0
| "D"  | "C"    | 4.0
| "E"  | "C"    | 6.0
|===
--

The algorithm finds a tree containing A,B,C,D, and E.
The node F is skipped  because it's price is very low and connecting it with the other nodes would yield an inferior solution.

=== Stats
:stats-syntax: algorithms-prize-steiner-tree-syntax

include::partial$/algorithms/shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the Prize-Collecting Steiner Tree algorithm in stats mode and return its statistics.
[source,cypher,role=noplay]
----
CALL gds.prizeSteinerTree.stats('graph', {
  relationshipWeightProperty: 'cost',
  prizeProperty: 'prize'
})
YIELD effectiveNodeCount, totalWeight, sumOfPrizes
RETURN effectiveNodeCount, totalWeight, sumOfPrizes
----

.Results
[opts="header"]
|===
| effectiveNodeCount | totalWeight | sumOfPrizes
| 5                  | 14.0        | 54.0
|===
--

The stats mode provides us with information about the total sum  weights of the relationships in the connected tree, which is `14.0`, as well as the sum of prizes for nodes A,B,C,D, and E, which is `54.0`.

=== Mutate

include::partial$/algorithms/shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the Prize-Collecting Steiner Tree algorithm in mutate mode and return its statistics.
[source,cypher,role=noplay]
----
CALL gds.prizeSteinerTree.mutate('graph', {
  relationshipWeightProperty: 'cost',
  prizeProperty: 'prize',
  mutateProperty: 'weight',
  mutateRelationshipType: 'STEINER'
})
YIELD effectiveNodeCount, totalWeight, sumOfPrizes, relationshipsWritten
RETURN effectiveNodeCount, totalWeight, sumOfPrizes, relationshipsWritten
----

.Results
[opts="header"]
|===
| effectiveNodeCount | totalWeight | sumOfPrizes | relationshipsWritten
| 5                  | 14.0        | 54.0 | 4
|===
--

The mutate mode updates the in-memory graph `graph` with  new relationship type
called `STEINER` with a single property `weight`.
From the `relationshipsWritten` column, we can see that exactly four such relationships were added.
They connect the nodes in the steiner tree, and their property is the cost of each connection.

[NOTE]
====
The relationships added back to the graph are always directed, even if the input graph is undirected. They point from `parent` to `node` in a fixed tree ordering.
====

=== Write

include::partial$/algorithms/shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the Prize-Collecting Steiner Tree algorithm in write mode and return its statistics.
[source,cypher,role=noplay]
----
CALL gds.prizeSteinerTree.write('graph', {
  relationshipWeightProperty: 'cost',
  prizeProperty: 'prize',
  writeProperty: 'weight',
  writeRelationshipType: 'STEINER'
})
YIELD effectiveNodeCount, totalWeight, sumOfPrizes, relationshipsWritten
RETURN effectiveNodeCount, totalWeight, sumOfPrizes, relationshipsWritten
----

.Results
[opts="header"]
|===
| effectiveNodeCount | totalWeight | sumOfPrizes | relationshipsWritten
| 5                  | 14.0        | 54.0 | 4
|===
--

This query writes back to the database four new relationships
each of type `STEINER` with a single property `weight`.

[NOTE]
====
The relationships added back  are always directed, even if the input graph is undirected. They point from `parent` to `node` in a fixed tree ordering.
====
