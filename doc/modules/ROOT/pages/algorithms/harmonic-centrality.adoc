[[algorithms-harmonic-centrality]]
= Harmonic Centrality
:description: This section describes the Harmonic Centrality algorithm
:entity: node
:result: score


:directed:
:undirected:
include::partial$/algorithms/shared/algorithm-traits.adoc[]


Harmonic centrality (also known as valued centrality) is a variant of closeness centrality, that was invented to solve the problem the original formula had when dealing with unconnected graphs.
As with many of the centrality algorithms, it originates from the field of social network analysis.


[[algorithms-harmonic-centrality-context]]
== History and explanation

Harmonic centrality was proposed by Marchiori and Latora in https://arxiv.org/pdf/cond-mat/0008357.pdf[Harmony in the Small World^] while trying to come up with a sensible notion of "average shortest path".

They suggested a different way of calculating the average distance to that used in the Closeness Centrality algorithm.
Rather than summing the distances of a node to all other nodes, the harmonic centrality algorithm sums the inverse of those distances.
This enables it deal with infinite values.

The *raw harmonic centrality* for a node is calculated using the following formula:

`raw harmonic centrality(node) = sum(1 / distance from node to every other node excluding itself)`

As with closeness centrality, we can also calculate a *normalized harmonic centrality* with the following formula:

`normalized harmonic centrality(node) = sum(1 / distance from node to every other node excluding itself) / (number of nodes - 1)`

In this formula, âˆž values are handled cleanly.


[[algorithms-harmonic-centrality-usecase]]
== Use-cases - when to use the Harmonic Centrality algorithm

Harmonic centrality was proposed as an alternative to closeness centrality, and therefore has similar use cases.

For example, we might use it if we're trying to identify where in the city to place a new public service so that it's easily accessible for residents.
If we're trying to spread a message on social media we could use the algorithm to find the key influencers that can help us achieve our goal.


[[algorithms-harmonic-centrality-syntax]]
== Syntax

:algorithm: Harmonic Centrality
include::partial$/algorithms/shared/syntax-intro-named-graph.adoc[]

.Harmonic Centrality syntax per mode
[.tabbed-example, caption = ]
====

[.include-with-stream]
======
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
CALL gds.closeness.harmonic.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  score: Float
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                          | Type    | Default                | Optional | Description
include::partial$/algorithms/common-configuration/common-stream-stats-configuration-entries.adoc[]
|===

.Results
[opts="header"]
|===
| Name   | Type    | Description
| nodeId | Integer | Node ID.
| score  | Float   | Harmonic centrality score.
|===

======

[.include-with-stats]
======
.The following will run the algorithm and return statistics:
[source, cypher, role=noplay]
----
CALL gds.closeness.harmonic.stats(
  graphName: String,
  configuration: Map
)
YIELD
  centralityDistribution: Map,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name             | Type    | Default                | Optional | Description
include::partial$/algorithms/common-configuration/common-configuration-jobid-concurrency-entries.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the statistics.
| configuration          | Map       | The configuration used for running the algorithm.
|===

======

[.include-with-mutate]
======
.The following will run the algorithm and update the graph:
[source, cypher, role=noplay]
----
CALL gds.closeness.harmonic.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  centralityDistribution: Map,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name             | Type    | Default                | Optional | Description
include::partial$/algorithms/common-configuration/common-configuration-jobid-concurrency-entries.adoc[]
include::partial$/algorithms/common-configuration/common-mutate-configuration-entries.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| mutateMillis          | Integer   | Milliseconds for adding properties to the projected graph.
| nodePropertiesWritten  | Integer   | Number of properties written to the projected graph.
| configuration          | Map       | The configuration used for running the algorithm.
|===

======

[.include-with-write]
======
.The following will run the algorithm and write back results:
[source, cypher, role=noplay]
----
CALL gds.closeness.harmonic.write(
  graphName: String,
  configuration: Map
)
YIELD
  centralityDistribution: Map,
  preProcessingMillis: Integer,
  computeMillis: Integer,
  postProcessingMillis: Integer,
  writeMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::partial$/algorithms/common-configuration/common-parameters.adoc[]

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name             | Type    | Default                | Optional | Description
include::partial$/algorithms/common-configuration/common-configuration-jobid-concurrency-entries.adoc[]
include::partial$/algorithms/common-configuration/common-write-configuration-entries.adoc[]
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type      | Description
| centralityDistribution | Map       | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of centrality values.
| preProcessingMillis    | Integer   | Milliseconds for preprocessing the graph.
| computeMillis          | Integer   | Milliseconds for running the algorithm.
| postProcessingMillis   | Integer   | Milliseconds for computing the statistics.
| writeMillis            | Integer   | Milliseconds for writing result data back.
| nodePropertiesWritten  | Integer   | Number of properties written to Neo4j.
| configuration          | Map       | The configuration used for running the algorithm.
|===

======

====

[[algorithms-harmonic-centrality-sample]]
== Harmonic Centrality algorithm example

.The following will create a sample graph:
[source, cypher, role=noplay setup-query]
----
CREATE (a:User {name: "Alice"}),
       (b:User {name: "Bob"}),
       (c:User {name: "Charles"}),
       (d:User {name: "Doug"}),
       (e:User {name: "Ethan"}),
       (a)-[:LINK]->(b),
       (b)-[:LINK]->(c),
       (d)-[:LINK]->(e)
----

.The following will project and store a named graph:
[source, cypher, role=noplay graph-project-query]
----
MATCH (source:User)-[r:LINK]->(target:User)
RETURN gds.graph.project(
  'graph',
  source,
  target,
  {},
  { undirectedRelationshipTypes: ['*'] }
)
----

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher, role=noplay]
----
CALL gds.closeness.harmonic.stream('graph', {})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS user, score
ORDER BY score DESC
----

.Results
[opts="header",cols="1,1"]
|===
| user      | score
| "Bob"     | 0.5
| "Alice"   | 0.375
| "Charles" | 0.375
| "Doug"    | 0.25
| "Ethan"   | 0.25
|===
--

[role=query-example]
--
.The following will run the algorithm and return statistics:
[source, cypher, role=noplay]
----
CALL gds.closeness.harmonic.stats('graph', {})
YIELD centralityDistribution
----

.Results
[opts="header"]
|===
| centralityDistribution
| {max=0.5000038147, mean=0.3500003815, min=0.25, p50=0.375, p75=0.375, p90=0.5000019073, p95=0.5000019073, p99=0.5000019073, p999=0.5000019073}
|===
--

[role=query-example]
--
.The following will run the algorithm and update the graph:
[source, cypher, role=noplay]
----
CALL gds.closeness.harmonic.mutate('graph', {mutateProperty: 'harmonicScore'})
YIELD nodePropertiesWritten, centralityDistribution
----

.Results
[opts="header"]
|===
| nodePropertiesWritten | centralityDistribution
| 5                     | {max=0.5000038147, mean=0.3500003815, min=0.25, p50=0.375, p75=0.375, p90=0.5000019073, p95=0.5000019073, p99=0.5000019073, p999=0.5000019073}
|===
--

[role=query-example]
--
.The following will run the algorithm and write back results:
[source, cypher, role=noplay]
----
CALL gds.closeness.harmonic.write('graph', {writeProperty: 'score'})
YIELD nodePropertiesWritten
----

.Results
[opts="header"]
|===
| nodePropertiesWritten
| 5
|===
--