[[alpha-algorithms-k-spanning-tree]]
[.alpha]
= Minimum Weight k-Spanning Tree
:description: This section describes the Minimum Weight k-Spanning Tree algorithm in the Neo4j Graph Data Science library.
:entity: node
:result: spanning tree edge
:algorithm: k-Spanning Tree heuristic

include::partial$/operations-reference/alpha-note.adoc[]


== Introduction

Sometimes,  we want to limit the size of our spanning tree result, as we are only interested in finding a smaller tree within the graph, and not one that necessarily spans across all nodes.
The K-Spanning tree heuristic algorithm returns a tree with `k` nodes and `k − 1` relationships.
Our heuristic processes the result found by the Prim algorithm for the Minimum Weight Spanning Tree problem.

[[algorithms-k-spanning]]
== Considerations
The minimum weight k-Spanning Tree is NP-Hard. The algorithm in the Neo4j GDS Library is therefore not guaranteed to find the optimal answer, but should return good approximation in practice.
[[algorithms-minimum-k-weight-spanning-tree-syntax]]
== Syntax

.The following will run the k-spanning tree algorithms and write back results:
[source, cypher, role=noplay]
----
CALL gds.alpha.kSpanningTree.write(
  graphName: String,
  configuration: Map
)
YIELD effectiveNodeCount: Integer,
      preProcessingMillis: Integer,
      computeMillis: Integer,
      writeMillis: Integer,
      configuration: Map
----

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                                                                             | Type    | Default | Optional  | Description
| k                                                                                | Integer | null    | no        | The result is a tree with `k` nodes and `k − 1` relationships.
| sourceNode                                                                      | Integer | null    | no        | The start node ID.
| xref:common-usage/running-algos.adoc#common-configuration-relationship-weight-property[relationshipWeightProperty] | String  | null    | yes       | Name of the relationship property to use as weights. If unspecified, the algorithm runs unweighted.
| writeProperty                                                                    | String  | n/a   | no       | The partition that a node belongs to.
| objective  | String | 'minimum'  | yes      | If specified, the parameter dictates whether to try and find the minimum or the maximum  k-spanning tree. By default, the algorithm looks for the minimum one. Permitted values are 'minimum' and 'maximum'.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                | Type    | Description
| effectiveNodeCount  | Integer | The number of visited nodes.
| preProcessingMillis | Integer | Milliseconds for preprocessing the data.
| computeMillis       | Integer | Milliseconds for running the algorithm.
| writeMillis         | Integer | Milliseconds for writing result data back.
|===

[[algorithms-minimum-weight-spanning-tree-sample]]
== Minimum Weight k-Spanning Tree algorithm examples

image::mst.png[]

.The following will create the sample graph depicted in the figure:
[source, cypher, role=noplay setup-query]
----
CREATE (a:Place {id: 'A'}),
       (b:Place {id: 'B'}),
       (c:Place {id: 'C'}),
       (d:Place {id: 'D'}),
       (e:Place {id: 'E'}),
       (f:Place {id: 'F'}),
       (g:Place {id: 'G'}),
       (d)-[:LINK {cost:4}]->(b),
       (d)-[:LINK {cost:6}]->(e),
       (b)-[:LINK {cost:1}]->(a),
       (b)-[:LINK {cost:3}]->(c),
       (a)-[:LINK {cost:2}]->(c),
       (c)-[:LINK {cost:5}]->(e),
       (f)-[:LINK {cost:1}]->(g);
----

.The following will project and store a named graph:
[source, cypher, role=noplay graph-project-query]
----
CALL gds.graph.project(
  'graph',
  'Place',
  {
    LINK: {
      properties: 'cost',
      orientation: 'UNDIRECTED'
    }
  }
)
----

[[algorithms-minimum-weight-spanning-tree-k]]
== K-Spanning tree examples

=== Minimum K-Spanning Tree example

In our sample graph we have 5 nodes.
By setting the `k=3`, we define that we want to get returned a 3-minimum spanning tree that covers 3 nodes and has 2 relationships.

.The following will run the k-minimum spanning tree algorithm and write back results:
[role=query-example, no-result=true, group=write-example]
--
[source, cypher, role=noplay]
----
MATCH (n:Place{id: 'D'})
CALL gds.alpha.kSpanningTree.write('graph', {
  k: 3,
  sourceNode: id(n),
  relationshipWeightProperty: 'cost',
  writeProperty:'kmin'
})
YIELD preProcessingMillis, computeMillis, writeMillis, effectiveNodeCount
RETURN preProcessingMillis,computeMillis,writeMillis, effectiveNodeCount;
----
--

[role=query-example, group=write-example]
--
.The following will find the nodes that belong to our k-spanning tree result:
[source, cypher, role=noplay]
----
MATCH (n)
WITH n.kmin AS p, count(n) AS c
WHERE c = 3
MATCH (n)
WHERE n.kmin = p
RETURN n.id As Place, p as Partition

----

.Results
[opts="header",cols="1,1"]
|===
| Place | Partition
| "A"     | 1
| "B"     | 1
| "C"     | 1
|===
--
Nodes A, B, and C are the result 3-minimum spanning tree of our graph.

=== Maximum K-Spanning Tree example

[role=query-example,no-result=true, group=max-example]
--
.The following will run the k-maximum spanning tree algorithm and write back results:
[source, cypher, role=noplay]
----
MATCH (n:Place{id: 'D'})
CALL gds.alpha.kSpanningTree.write('graph', {
  k: 3,
  sourceNode: id(n),
  relationshipWeightProperty: 'cost',
  writeProperty:'kmax',
  objective: 'maximum'
})
YIELD preProcessingMillis, computeMillis, writeMillis, effectiveNodeCount
RETURN preProcessingMillis,computeMillis,writeMillis, effectiveNodeCount;
----
--

[role=query-example, group=max-example]
--
.Find nodes that belong to our k-spanning tree result:
[source, cypher, role=noplay]
----
MATCH (n)
WITH n.kmax AS p, count(n) AS c
WHERE c = 3
MATCH (n)
WHERE n.kmax = p
RETURN n.id As Place, p as Partition
----
.Results
[opts="header",cols="1,1"]
|===
| Place | Partition
| "C"     | 3
| "D"     | 3
| "E"     | 3
|===
--
Nodes C, D, and E are the result 3-maximum spanning tree of our graph.
