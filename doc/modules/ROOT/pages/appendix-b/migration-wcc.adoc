[[migration-wcc]]
= Weakly Connected Components

.Changes in Configuration
[opts=header]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
| `direction`                         | -
| `concurrency`                       | `concurrency`
| `readConcurrency`                   | `readConcurrency` footnote:wcc-read[Only when using anonymous graph]
| `writeConcurrency`                  | `writeConcurrency` footnote:wcc-write[Only for `write` mode]
| `writeProperty`                     | `writeProperty` footnote:wcc-write[]
| `weightProperty`                    | `relationshipWeightProperty`
| `defaultValue`                      | `defaultValue`
| `seedProperty`                      | `seedProperty`
| `threshold`                         | `threshold`
| `consecutiveIds`                    | `consecutiveIds`
| `write`                             | -
| `graph`                             | -
|===

.Changes in YIELD fields
[opts=header]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
| `loadMillis`             | `createMillis`
| `computeMillis`          | `computeMillis`
| `writeMillis`            | `writeMillis`
| `postProcessingMillis`   | `postProcessingMillis`
| `nodeId`                 | `nodeId` footnote:wcc-stream-yield[Only for `stream` mode]
| `setId`                  | `componentId` footnote:wcc-stream-yield[]
| `nodes`                  | `nodePropertiesWritten` footnote:wcc-write-yield[Only for `write` mode]
| -                      | `relationshipPropertiesWritten` footnote:wcc-write-yield[]
| `write`                  | -
| -                      | `componentDistribution`
| -                      | `configuration` footnote:wcc-gds-config[The configuration used to run the algorithm]
| `writeProperty` footnote:wcc-config[Inlined into `configuration`]         | -
| `weightProperty` footnote:wcc-weight-config[Inlined into `configuration` as `relationshipWeightProperty`]         | -
| `min, max, mean, p50, p75, p90, p95, p99, p999` footnote:wcc-distribution[Inlined into `componentDistribution`] | -
|===

.Weakly Connected Components Stream Mode
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
2+| Minimalistic stream over named graph:
|
[source, cypher, role=noplay]
----
CALL algo.unionFind.stream(null, null, {graph: 'myGraph'})
YIELD nodeId, setId
----
|
[source, cypher, role=noplay]
----
CALL gds.wcc.stream('myGraph')
YIELD nodeId, componentId
----
2+| Streaming over weighted named graph:
|
[source, cypher, role=noplay]
----
CALL algo.unionFind.stream(
  null,
  null,
  {
    graph: 'myGraph',
    weightProperty: 'myWeightProperty'
  }
)
----
|
[source, cypher, role=noplay]
----
CALL gds.wcc.stream(
  'myGraph',
  {
    relationshipWeightProperty: 'myWeightProperty'
  }
)
----
2+| Minimalistic streaming over anonymous graph:
|
[source, cypher, role=noplay]
----
CALL algo.unionFind.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE'
)
----
|
[source, cypher, role=noplay]
----
CALL gds.wcc.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: 'MY_RELATIONSHIP_TYPE'
})
----
2+| Streaming over anonymous graph with `REVERSE` relationship orientation:
|
[source, cypher, role=noplay]
----
CALL algo.unionFind.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  { direction: 'INCOMING' }
)
----
|
[source, cypher, role=noplay]
----
CALL gds.wcc.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: {
    MY_RELATIONSHIP_TYPE: {
      orientation: 'REVERSE'
    }
  }
})
----
2+| Streaming over anonymous graph with relationship specifying default value for the weight property:
|
[source, cypher, role=noplay]
----
CALL algo.unionFind.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  {
    graph: 'myGraph',
    weightProperty: 'myWeightProperty',
    defaultValue: 2.0
  }
)
----
|
[source, cypher, role=noplay]
----
CALL gds.wcc.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: {
    MY_RELATIONSHIP_TYPE: {
      properties: {
        myWeightProperty: {
          defaultValue: 2
        }
      }
    }
  }
})
----
|===

.Weakly Connected Components Write Mode
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
2+| Minimalistic `write` mode:
|
[source, cypher, role=noplay]
----
CALL algo.unionFind(
  null,
  null,
  {
    graph: 'myGraph',
    writeProperty: 'myWriteProperty',
    write: true
  }
)
YIELD
  nodes,
  loadMillis,
  p1,
  writeProperty
----
|
[source, cypher, role=noplay]
----
CALL gds.wcc.write(
  'myGraph',
  { writeProperty: 'myWriteProperty' }
)
YIELD
  nodePropertiesWritten,
  createMillis,
  componentDistribution AS cd,
  configuration AS conf
RETURN
  nodePropertiesWritten,
  createMillis,
  cd.p1 AS p1,
  conf.writeProperty AS writeProperty
----
2+| Running `write` mode over weighted named graph:
|
[source, cypher, role=noplay]
----
CALL algo.unionFind(
  null,
  null,
  {
    graph: 'myGraph',
    writeProperty: 'myWriteProperty',
    weightProperty: 'myWeightProperty',
    write: true
  }
)
----
|
[source, cypher, role=noplay]
----
CALL gds.wcc.write(
  'myGraph',
  {
    writeProperty: 'myWriteProperty',
    relationshipWeightProperty: 'myWeightProperty'
  }
)
----
2+| Memory estimation of the algorithm:
|
[source, cypher, role=noplay]
----
CALL algo.memrec(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  'unionFind',
  {
    writeProperty: 'myWriteProperty',
    weightProperty: 'myRelationshipWeightProperty',
    write: true
  }
)
----
|
[source, cypher, role=noplay]
----
CALL gds.wcc.write.estimate(
  {
    nodeProjection: 'MyLabel',
    relationshipProjection: 'MY_RELATIONSHIP_TYPE',
    writeProperty: 'myWriteProperty',
    relationshipWeightProperty: 'myWeightProperty'
  }
)
----
|===
